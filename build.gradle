/*
 * Copyright 2017 Google, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    ext.versions = [
            'android_gradle' : '3.2.0-beta04',
            'androidx'       : [
                    'appcompat'        : '28.0.0-alpha3',
                    'browser'          : '28.0.0-alpha3',
                    'core'             : '1.1.1',
                    'constraint_layout': '2.0.0-alpha1',
                    'core_ktx'         : '0.3',
                    'fragment'         : '28.0.0-alpha3',
                    'emoji'            : '28.0.0-alpha3',
                    'lifecycle'        : '1.1.1',
                    'paging'           : '1.0.1',
                    'palette'          : '28.0.0-alpha3',
                    'recyclerview'     : '28.0.0-alpha3',
                    'room'             : '1.1.1',
                    'test_runner'      : '1.0.2',
                    'test_rules'       : '1.0.2',
            ],
            'coroutines'     : '0.23.4',
            'dagger'         : '2.16',
            'epoxy'          : '2.14.0',
            'espresso'       : '3.0.2',
            'glide'          : '4.7.1',
            'kotlin'         : '1.2.51',
            'ktlint'         : '0.19.0',
            'mockito'        : '2.18.3',
            'material_design': '28.0.0-alpha3',
            'rx'             : '2.1.16',
            'rxkotlin'       : '2.2.0',
            'rxlint'         : '1.6.1',
            'threetenabp'    : '1.1.0',
            'retrofit'       : '2.4.0',
    ]

    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 23,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'

        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'

        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.13.0"
    id 'com.github.ben-manes.versions' version "0.20.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
