/*
 * Copyright 2017 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

import app.tivi.buildsrc.Libs
import app.tivi.buildsrc.Versions

buildscript {
    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 23,
            'targetSdk' : 29,
    ]

    repositories {
        google()
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        jcenter()

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        classpath Libs.androidGradlePlugin

        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Kotlin.extensions

        classpath Libs.gradlePlayPublisher

        classpath Libs.Google.gmsGoogleServices
        classpath Libs.Google.crashlyticsGradle
        classpath Libs.Google.openSourceLicensesPlugin

        classpath Libs.AndroidX.Navigation.safeArgs

        classpath Libs.Hilt.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '4.3.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()

        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }

        if (Libs.AndroidX.Compose.version.endsWith("SNAPSHOT")) {
            maven {
                url "https://androidx.dev/snapshots/builds/${Libs.AndroidX.Compose.snapshot}/artifacts/ui/repository/"
            }
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }

        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    // Compose SNAPSHOTs currently depend on Kotlin 1.4.0 (which doesn't exist yet). We need to
    // force a version which does exist.
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    // kotlin-stdlib-jre7 no longer exists in 1.4, so we force the
                    // kotlin-stdlib module instead
                    if (details.requested.module.name == 'kotlin-stdlib-jre7') {
                        details.useTarget Libs.Kotlin.stdlib
                    } else {
                        details.useVersion Libs.Kotlin.version
                    }
                }
            }
        }
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
            freeCompilerArgs += "-Xopt-in=kotlin.Experimental"

            // Since we're using a pre-release version of the Kotlin compiler, we need
            // to disable it's pre-release check
            freeCompilerArgs += "-Xskip-prerelease-check"

            freeCompilerArgs += "-Xallow-jvm-ir-dependencies"

            // Set JVM target to 1.8
            jvmTarget = "1.8"

            // Need to keep the API version to 1.3 for certain dependencies:
            // Store: https://github.com/dropbox/Store/issues/188
            apiVersion = "1.3"
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
