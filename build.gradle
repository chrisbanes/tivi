/*
 * Copyright 2017 Google, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    ext.versions = [
            'android_gradle'   : '3.0.1',
            'support_library'  : '27.1.0',
            'constraint_layout': '1.1.0-beta5',
            'arch'             : '1.1.0',
            'arch_paging'      : '1.0.0-alpha6',
            'arch_room'        : '1.1.0-alpha3',
            'core_ktx'         : '0.2',
            'kotlin'           : '1.2.30',
            'espresso'         : '3.0.1',
            'dagger'           : '2.15',
            'ktlint'           : '0.19.0',
            'leakcanary'       : '1.5.4',
            'rx'               : '2.1.10',
            'rxlint'           : '1.6.1',
            'firebase'         : '11.8.0',
            'epoxy'            : '2.10.0'
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'

        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'

        classpath 'io.fabric.tools:gradle:1.25.1'

        classpath 'com.google.firebase:firebase-plugins:1.1.5'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.10.0"
    id 'com.github.ben-manes.versions' version "0.17.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

String propOrDef(String propertyName, String defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
