/*
 * Copyright 2017 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

import app.tivi.buildsrc.Libs
import app.tivi.buildsrc.Versions

buildscript {
    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 23,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }

        jcenter()
    }

    dependencies {
        classpath Libs.androidGradlePlugin

        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Kotlin.extensions

        classpath Libs.dexcountGradlePlugin
        classpath Libs.playPublisherPlugin

        classpath Libs.Google.gmsGoogleServices
        classpath Libs.Google.fabricPlugin

        classpath Libs.AndroidX.Navigation.safeArgs
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.21.1"
    id 'com.github.ben-manes.versions' version "0.21.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        // Required for Room 2.1.0-alpha05 due to https://youtrack.jetbrains.com/issue/KT-27991
        maven { url "https://kotlin.bintray.com/kotlinx/" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
