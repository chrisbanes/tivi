/*
 * Copyright 2017 Google, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    ext.versions = [
            'android_gradle' : '3.2.1',
            'androidx'       : [
                    'appcompat'        : '1.0.0',
                    'browser'          : '1.0.0',
                    'core'             : '2.0.0',
                    'constraint_layout': '2.0.0-alpha2',
                    'core_ktx'         : '1.0.0',
                    'fragment'         : '1.0.0',
                    'emoji'            : '1.0.0',
                    'lifecycle'        : '2.0.0',
                    'paging'           : '2.0.0-rc01',
                    'palette'          : '1.0.0',
                    'recyclerview'     : '1.0.0',
                    'room'             : '2.0.0-rc01',
                    'test_runner'      : '1.1.0-alpha4',
                    'test_rules'       : '1.1.0-alpha4',
                    'work'             : '1.0.0-alpha09'
            ],
            'coroutines'     : '0.30.2-eap13',
            'dagger'         : '2.16', // Pinning this due to https://issuetracker.google.com/115738511
            'epoxy'          : '3.0.0-rc1',
            'espresso'       : '3.1.0-alpha4',
            'firebase'       : '16.0.3',
            'glide'          : '4.8.0',
            'kotlin'         : '1.3.0-rc-146',
            'ktlint'         : '0.28.0',
            'mockito'        : '2.18.3',
            'material_design': '1.0.0',
            'rx'             : '2.2.2',
            'rxkotlin'       : '2.3.0',
            'rxlint'         : '1.6.1',
            'threetenbp'     : '1.3.7',
            'threetenabp'    : '1.1.0',
            'retrofit'       : '2.4.0',
            'junit'          : '4.12',
            'robolectric'    : '4.0-beta-1',
            'mockito_kotlin' : '2.0.0-RC3'
    ]

    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 23,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }

        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'

        classpath 'com.github.triplet.gradle:play-publisher:2.0.0-beta1'

        classpath 'com.google.gms:google-services:4.1.0'
        classpath 'io.fabric.tools:gradle:1.26.0'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.14.0"
    id 'com.github.ben-manes.versions' version "0.20.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
